## title: Ligand-induced dimerization of receptor model
## author: Jim Faeder
## date: 22April2015

begin model
begin parameters
 Na 6.02e23 # Avogadro's number
 Vec 1e-9   # Volume of extracellular space
 lig_conc 1e-9 # Ligand concentration - molar 
 L0 lig_conc*Na*Vec # number of ligand molecules
 R0 10000 # number of receptor molecules
 
 kp1 1e8/(Na*Vec) # Forward binding rate constant for L-R
 km1 0.1 # Reverse binding rate constant for L-R
 kp2 1000*kp1 # Forward binding rate constant for surface-bound L
 km2 km1 # Reverse binding rate for L-R in dimer
 
end parameters

begin molecule types
  L(r,r) # L molecule has two binding sites for R
  R(l) # R molecule has one binding site for L
end molecule types

begin species
  L(r,r) 0.5*L0
  R(l) R0
end species

begin observables
  Molecules FreeR R(l)
  Molecules Bound L(r!1,r).R(l!1)
  Molecules DimR  R(l!1).L(r!1,r!2).R(l!2)  # gives the number of receptors in dimers
  Species   Dimers R(l!1).L(r!1,r!2).R(l!2) # gives the number of dimer complexes
  Molecules DimR2 R(l!1).L(r!1,r!+) # Another way of specifying DimR using wildcards
end observables

begin reaction rules
  LRbind: L(r,r) + R(l) <-> L(r,r!1).R(l!1) kp1, km1
  LRdimer: L(r,r!1).R(l!1) + R(l) <-> L(r!2,r!1).R(l!1).R(l!2) kp2, km2 
end reaction rules
end model

generate_network()
simulate({method=>"ode",t_end=>500,n_steps=>500})